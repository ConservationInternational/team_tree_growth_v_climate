model {
    ###########################################################################
    # Parameters:
    #   B[1]: fixed effect - wood density
    #   B[2]: fixed effect - wood density (squared)
    #   int_ijk: random intercept - tree level
    #   int_jk: random intercept - plot level
    #   int_k: random intercept - site level
    #   B_g[g, 1]: random intercept for genus g
    #   B_g[g, 2]: random slope on precip genus g
    #   B_g[g, 3]: random slope on precip (squared) genus g
    #   B_g[g, 4]: random slope on temperature genus g
    #   B_g[g, 5]: random slope on temperature (squared) genus g
    #   B_g[g, 6]: random slope on initial diameter genus g
    #   B_g[g, 7]: random slope on initial diameter (squared) genus g
    #   B_g[g, 8]: random slope on interaction of density and precip
    #   B_g[g, 9]: random slope on interaction of initial diameter and precip
    #   B_g[g, 10]: random slope on interaction of density and temp
    #   B_g[g, 11]: random slope on interaction of initial diameter and temp
    #   B_T[k, 1]: intercept in temperature model for site k
    #   B_T[k, 2]: lapse rate in temperature model for site k
    #   sigma_obs: std. dev. of observation error
    #   sigma_proc: std. dev. of residual error
    #   sigma_ijk: std. dev. of tree-level random intercept
    #   sigma_jk: std. dev. of plot-level random intercept
    #   sigma_k: std. dev. of site-level random intercept
    #   sigma_t: std. dev. of period random intercept
    #   sigma_B_g: variances of genus-level random effects (uncorrelated)
    #   mu_B_g: mean of genus-level random effects
    ###########################################################################

    ##########################################################################
    # Likelihood
    for (i in 1:n_tree) {
        for (t in (first_obs_period[i] + 1):last_obs_period[i]) {
            dbh_predicted[i, t] <- B[1] * WD[i] +
                B[2] * WD_sq[i] +
                int_ijk[i] +
                int_jk[plot_ID[i]] +
                int_k[site_ID[i]] +
                B_g[genus_ID[i], 1] +
                B_g[genus_ID[i], 2] * precip[i, t] +
                B_g[genus_ID[i], 3] * precip_sq[i, t] +
                B_g[genus_ID[i], 4] * (temp[i, t] + B_T_int[site_ID[i]] + B_T_lapse[site_ID[i]] * elev[plot_ID[i]]) +
                B_g[genus_ID[i], 5] * pow(temp[i, t] + B_T_int[site_ID[i]] + B_T_lapse[site_ID[i]] * elev[plot_ID[i]], 2) +
                B_g[genus_ID[i], 6] * dbh_latent[i, t - 1] +
                B_g[genus_ID[i], 7] * pow(dbh_latent[i, t - 1], 2) +
                B_g[genus_ID[i], 8] * precip[i, t] * WD[i] +
                B_g[genus_ID[i], 9] * precip[i, t] * dbh_latent[i, t - 1] +
                B_g[genus_ID[i], 10] * (temp[i, t] + B_T_int[site_ID[i]] + B_T_lapse[site_ID[i]] * elev[plot_ID[i]]) * WD[i] +
                B_g[genus_ID[i], 11] * (temp[i, t] + B_T_int[site_ID[i]] + B_T_lapse[site_ID[i]] * elev[plot_ID[i]]) * dbh_latent[i, t - 1]

            dbh_latent[i, t] ~ dnorm(dbh_predicted[i, t], prec_proc)
        }

        for (t in (first_obs_period[i]):last_obs_period[i]) {
            dbh[i, t] ~ dnorm(dbh_latent[i, t], prec_obs)
        }

        # Handle first latent dbh (for which there is no prediction)
        dbh_latent[i, first_obs_period[i]] ~ dnorm(0, 1.0E-4)
    }

    ##########################################################################
    # Fixed effects
    for (k in 1:n_B) {
        B[k] ~ dnorm(0, 1.0E-4)
    }

    ##########################################################################
    # Observation and process error
    prec_obs <- pow(sigma_obs, -2)
    # Base sigma_obs on precision of dbh measurements (following Eitzel 2013)
    sigma_obs ~ dunif(sigma_obs_lower, 1)

    prec_proc <- pow(sigma_proc, -2)
    sigma_proc ~ dunif(0, 10)

    ##########################################################################
    # Nested random effects
    for (i in 1:n_tree) {
        int_ijk[i] ~ dnorm(0, tau_int_ijk)
    }
    tau_int_ijk <- pow(sigma_int_ijk, -2)
    sigma_int_ijk ~ dunif(0, 10)

    for (j in 1:n_plot) {
        int_jk[j] ~ dnorm(0, tau_int_jk)
    }
    tau_int_jk <- pow(sigma_int_jk, -2)
    sigma_int_jk ~ dunif(0, 10)

    for (k in 1:n_site) {
        int_k[k] ~ dnorm(0, tau_int_k)
    }
    tau_int_k <- pow(sigma_int_k, -2)
    sigma_int_k ~ dunif(0, 10)

    ##########################################################################
    # Random effects at genus level (crossed).
    for (k in 1:n_B_g) {
        mu_B_g[k] ~ dnorm(0, 1.0E-4)
        tau_B_g[k] <- pow(sigma_B_g[k], -2)
        sigma_B_g[k] ~ dunif(0, 10)
        for (g in 1:n_genus) {
            B_g[g, k] ~ dnorm(mu_B_g[k], tau_B_g[k])
        }
    }

    ##########################################################################
    # Temperature model
    for (k in 1:n_site) {
        # Prior on intercept
        B_T_int[k] ~ dnorm(0, 1.0E-4)
        # Prior on lapse rate (recall lapse rate is defined as rate of decrease 
        # of temp with increase in elevation, so the below truncation to make 
        # B_T[k, 2] less than zero means that lapse rate is constrained to be 
        # positive).
        B_T_lapse[k] ~ dnorm(lapse_mean, lapse_prec) I(, 0)
    }
}
