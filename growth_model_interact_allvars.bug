model {
    ###########################################################################
    # Parameters:
    #   int_jk: random intercept - plot level
    #   int_k: random intercept - site level
    #   int_t: random intercept - by period
    #   B_g[g, 1]: random intercept for genus g
    #   B_g[g, 2]: random slope on precip genus g
    #   B_g[g, 3]: random slope on precip (squared) genus g
    #   B_g[g, 4]: random slope on temperature genus g
    #   B_g[g, 5]: random slope on temperature (squared) genus g
    #   B_g[g, 6]: random slope on initial diameter genus g
    #   B_g[g, 7]: random slope on initial diameter (squared) genus g
    #   B_g[g, 8]: random slope on interaction of initial diameter and precip
    #   B_g[g, 9]: random slope on interaction of initial diameter and temp
    #   B_k: slope on difference in elev. of plot from mean CRU cell elev.
    #   sigma_obs: std. dev. of observation error
    #   sigma_proc: std. dev. of residual error
    #   sigma_B_k: std. dev. of site-level slope on elev. difference
    #   sigma_int_jk: std. dev. of plot-level random intercept
    #   sigma_int_k: std. dev. of site-level random intercept
    #   sigma_int_t: std. dev. of period random intercept
    #   sigma_B_g: variances of genus-level random effects (uncorrelated)
    #   mu_B_g: mean of genus-level random effects
    ###########################################################################

    ##########################################################################
    # Likelihood
    for (i in 1:n_tree) {
        for (t in (first_obs_period[i] + 1):last_obs_period[i]) {
            dbh_predicted[i, t] <- int_jk[plot_ID[i]] +
                int_k[site_ID[i]] +
                int_t[t - 1] +
                B_g[genus_ID[i], 1] +
                B_g[genus_ID[i], 2] * precip[i, t] +
                B_g[genus_ID[i], 3] * precip_sq[i, t] +
                B_g[genus_ID[i], 4] * temp_min[i, t] +
                B_g[genus_ID[i], 5] * temp_min_sq[i, t] +
                B_g[genus_ID[i], 6] * temp_mean[i, t] +
                B_g[genus_ID[i], 7] * temp_mean_sq[i, t] +
                B_g[genus_ID[i], 8] * temp_max[i, t] +
                B_g[genus_ID[i], 9] * temp_max_sq[i, t] +
                B_g[genus_ID[i], 10]* dbh_latent[i, t - 1] +
                B_g[genus_ID[i], 11] * pow(dbh_latent[i, t - 1], 2) +
                B_g[genus_ID[i], 12] * precip[i, t] * dbh_latent[i, t - 1] +
                B_g[genus_ID[i], 13] * temp_min[i, t] * dbh_latent[i, t - 1] +
                B_g[genus_ID[i], 14] * temp_mean[i, t] * dbh_latent[i, t - 1] +
                B_g[genus_ID[i], 15] * temp_max[i, t] * dbh_latent[i, t - 1] +
                B_k[site_ID[i]] * elev_diff[plot_ID[i]]

            dbh_latent[i, t] ~ dnorm(dbh_predicted[i, t], prec_proc)
        }

        for (t in (first_obs_period[i]):last_obs_period[i]) {
            dbh[i, t] ~ dnorm(dbh_latent[i, t], prec_obs)
        }

        # Handle first latent dbh (for which there is no prediction)
        dbh_latent[i, first_obs_period[i]] ~ dnorm(0, 1.0E-4)
    }

    ##########################################################################
    # Observation and process error
    prec_obs <- pow(sigma_obs, -2)
    # Base sigma_obs on precision of dbh measurements (following Eitzel 2013)
    sigma_obs ~ dunif(sigma_obs_lower, 1)

    prec_proc <- pow(sigma_proc, -2)
    sigma_proc ~ dunif(0, 10)

    ##########################################################################
    # Nested random effects
    for (j in 1:n_plot) {
        int_jk[j] ~ dnorm(0, tau_int_jk)
    }
    tau_int_jk <- pow(sigma_int_jk, -2)
    sigma_int_jk ~ dunif(0, 10)

    for (k in 1:n_site) {
        B_k[k] ~ dnorm(0, tau_B_k)
        int_k[k] ~ dnorm(0, tau_int_k)
    }
    tau_B_k <- pow(sigma_B_k, -2)
    sigma_B_k ~ dunif(0, 10)
    tau_int_k <- pow(sigma_int_k, -2)
    sigma_int_k ~ dunif(0, 10)

    ##########################################################################
    # Period random effects (crossed)
    for (t in 1:n_period) {
        int_t[t] ~ dnorm(0, tau_int_t)
    }
    tau_int_t <- pow(sigma_int_t, -2)
    sigma_int_t ~ dunif(0, 10)

    ##########################################################################
    # Random effects at genus level (crossed).
    for (k in 1:n_B_g) {
        mu_B_g[k] ~ dnorm(0, 1.0E-4)
        tau_B_g[k] <- pow(sigma_B_g[k], -2)
        sigma_B_g[k] ~ dunif(0, 10)
        for (g in 1:n_genus) {
            B_g[g, k] ~ dnorm(mu_B_g[k], tau_B_g[k])
        }
    }
}
